package UnitWrapper

import UnitIndexer

/// Wrapper around user that automatically is destroyed when unit is removed or decayed.
public class UnitWrapper 
    unit u
    static hashtable instances

    /// Wrap existing unit
    construct(unit pu)
        u = pu 
        instances.saveInt(pu.getHandleId(), 0, this castTo int)

    ondestroy
        instances.removeInt(u.getHandleId(), 0)
        u.remove()
    
    /// Allows to find UnitWrapper that is associated with given unit (or null if no such object)
    static function getInstanceThatBelongsTo(unit pu) returns UnitWrapper
        return instances.loadInt(pu.getHandleId(), 0) castTo UnitWrapper

    /// Get internal unit
    function getUnit() returns unit 
        return u 

init
    UnitWrapper.instances = InitHashtable()
    onUnitDeindex() -> 
        let o = UnitWrapper.getInstanceThatBelongsTo(getDeindexedUnit())
        if o != null 
            destroy o